# Maintainer: Marco Cantoro <marco dot cantoro92 at outlook dot it>

_pkgbase=suckless
pkgname=tabbed-git
pkgver=0.6
pkgrel=1
pkgdesc="Simple generic tabbed frontend to xembed-aware applications"
arch=('x86_64')
url="https://tools.suckless.org/tabbed"
license=('MIT')
depends=(
  'expat'
  'fontconfig'
  'freetype2'
  'glib2'
  'graphite'
  'harfbuzz'
  'libpng'
  'libx11'
  'libxau'
  'libxcb'
  'libxdmcp'
  'libxft'
  'libxrender'
  'pcre'
  'zlib'
)
makedepends=('git')
provides=("${pkgname%-git}" "xembed")
conflicts=("${pkgname%-git}" "xembed")
source=('git+https://github.com/CantoroMC/suckless')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgbase"
  printf "0.6.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgbase/${pkgname%-git}"
  make
}

package() {
  local licdir="$pkgdir/usr/share/licenses/${pkgname%-git}"
  local docdir="$pkgdir/usr/share/doc/${pkgname%-git}"

  cd "$srcdir/$_pkgbase/${pkgname%-git}"
  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -Dm0644 --target-directory "$licdir" "$srcdir/$_pkgbase/${pkgname%-git}/LICENSE"
  install -Dm0644 --target-directory "$docdir" "$srcdir/$_pkgbase/${pkgname%-git}/README"
  install -Dm0644 --target-directory "$docdir" "$srcdir/$_pkgbase/${pkgname%-git}/TODO"
}
