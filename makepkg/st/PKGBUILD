# Maintainer: Marco Cantoro <marco dot cantoro92 at outlook dot it>

_pkgbase=suckless
pkgname=st-git
pkgver=0.8.4
pkgrel=1
pkgdesc="A simple terminal implementation for X"
arch=('x86_64')
url="https://st.suckless.org"
license=('MIT')
depends=(
  'expat'
  'fontconfig'
  'freetype2'
  'glib2'
  'graphite'
  'harfbuzz'
  'libpng'
  'libxau'
  'libxcb'
  'libxdmcp'
  'libx11'
  'libxft'
  'libxrender'
  'pcre'
  'zlib'
)
makedepends=('git')
optdepends=('dmenu: for integration with key-bindings')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/CantoroMC/suckless')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgbase"
  printf "0.8.4.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgbase/${pkgname%-git}/src"
  make
}

package() {
  local licdir="$pkgdir/usr/share/licenses/${pkgname%-git}"
  local docdir="$pkgdir/usr/share/doc/${pkgname%-git}"

  cd "$srcdir/$_pkgbase/${pkgname%-git}/src"
  make PREFIX=/usr DESTDIR="$pkgdir" install

  install --mode 0644 -D --target-directory "$licdir" "$srcdir/$_pkgbase/${pkgname%-git}/LICENSE"
  install --mode 0644 -D --target-directory "$docdir" "$srcdir/$_pkgbase/${pkgname%-git}/README.md"
  install --mode 0644 -D --target-directory "$docdir" "$srcdir/$_pkgbase/${pkgname%-git}/FAQ"
  install --mode 0644 -D --target-directory "$docdir" "$srcdir/$_pkgbase/${pkgname%-git}/LEGACY"
  install --mode 0644 -D --target-directory "$docdir" "$srcdir/$_pkgbase/${pkgname%-git}/TODO.md"
}
