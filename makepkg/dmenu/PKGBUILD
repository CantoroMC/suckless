# Maintainer: Marco Cantoro <marco dot cantoro92 at outlook dot it>

_pkgbase=suckless
pkgname=dmenu-git
pkgver=5.0
pkgrel=1
pkgdesc="A dynamic menu for X"
arch=('x86_64')
url="https://tools.suckless.org/dmenu"
license=('MIT')
depends=(
  'expat'
  'fontconfig'
  'freetype2'
  'glib2'
  'graphite'
  'harfbuzz'
  'libpng'
  'libxau'
  'libxcb'
  'libxdmcp'
  'libx11'
  'libxft'
  'libxrender'
  'pcre'
  'zlib'
)
makedepends=('git')
provides=("${pkgname%-git}" "stest")
conflicts=("${pkgname%-git}" "stest")
source=('git+https://github.com/CantoroMC/suckless')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgbase"
  printf "5.0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgbase/${pkgname%-git}"
  make
}

package() {
  local licdir="$pkgdir/usr/share/licenses/${pkgname%-git}"
  local docdir="$pkgdir/usr/share/doc/${pkgname%-git}"

  cd "$srcdir/$_pkgbase/${pkgname%-git}"
  make PREFIX=/usr DESTDIR="$pkgdir" install

  install --mode 0644 -D --target-directory "$licdir" "$srcdir/$_pkgbase/${pkgname%-git}/LICENSE"
  install --mode 0644 -D --target-directory "$docdir" "$srcdir/$_pkgbase/${pkgname%-git}/README.md"
}
